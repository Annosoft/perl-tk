#! /usr/bin/env perl
use strict;
use warnings;
use Cwd 'abs_path';
use lib ();

=head1 NAME

tkhack - bring in a hacked Perl-Tk

=head1 SYNOPSIS

 cd /foo/perl-tk
 perl Makefile.PL
 make

 blib/script/tkhack ./myprog

=cut


sub main {
    my @nextprog = @_;
    my $prog = abs_path($0);

    my ($base, $inst) = $prog =~ m{^(.*)(blib/script|bin)/tkhack$}
      or die "Can't get base from $prog";

    my @add = $inst =~ /blib/ ? qw( blib/arch blib/lib ) : qw( lib/perl5 );
    @add = map {"$base/$_"} @add;
    lib->import(@add);

    die qq{perl-tk not found at $INC[0] under base=$base}
      unless -f "$INC[0]/auto/Tk/Tk.so";

    $ENV{ZMAP_LIB} = join ':', @add;
    $ENV{PERL5LIB} = join ':', @add,
      $ENV{PERL5LIB};

    die "Oops, PERL5OPT already set?!" if defined $ENV{PERL5OPT};
    $ENV{PERL5OPT} = '-MTk::AutoTrace';

    exec @nextprog;
    die "exec $nextprog[0] failed: $!";
}

exit main(@ARGV);
